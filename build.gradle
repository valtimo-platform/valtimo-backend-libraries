buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://repo.spring.io/plugins-release" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}"
    }
}

plugins {
    id "net.ltgt.apt-idea" version "0.21"
    id "net.ltgt.apt" version "0.21"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id "org.liquibase.gradle" version "2.0.4"
    id "com.github.ben-manes.versions" version "0.20.0"
    id "org.owasp.dependencycheck" version "5.0.0-M1"
    id "org.sonarqube" version "2.7"
    id "checkstyle"
    id "jacoco"
    id "io.freefair.lombok" version "5.3.0"
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
    id "org.jetbrains.kotlin.plugin.spring" version "1.4.21"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.4.21"
}

/*
 * Copyright 2015-2020 Ritense BV, the Netherlands.
 *
 * Licensed under EUPL, Version 1.2 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://joinup.ec.europa.eu/collection/eupl/eupl-text-eupl-12
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" basis,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//Jacoco
jacoco {
    toolVersion = "0.8.3"
}

def allTestCoverageFile = "${rootProject.buildDir}/jacoco/allTestCoverage.exec"
task jacocoMerge(type: JacocoMerge, group: 'verification') {
    destinationFile = file(allTestCoverageFile)
    executionData = project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
}

//Sonarqube
sonarqube {
    properties {
        property "sonar.scm.provider", "git"
        property "sonar.jacoco.reportPaths", allTestCoverageFile
    }
}

configurations {
    testImplementation {
        // Globally exclude JUnit4, will not be on the testCompileClasspath thus
        exclude group: 'junit', module: 'junit'
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "software.amazon.awssdk:bom:2.15.62"
            mavenBom "org.testcontainers:testcontainers-bom:1.15.1"
            mavenBom "com.fasterxml.jackson:jackson-bom:2.12.1"
            mavenBom "org.junit:junit-bom:5.7.0"
        }
    }

    lombok {
        version = "1.18.16"
    }

    bootJar.enabled = false

    sourceCompatibility = JavaVersion.VERSION_13
    targetCompatibility = JavaVersion.VERSION_13
    group = projectGroup
    version = projectVersion

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "http://repo.ritense.com/repository/maven-public/" }
    }

}

apply from: "gradle/deployment.gradle"

subprojects { subproject ->
    apply plugin: "idea"
    apply plugin: "java-library"
    apply plugin: "checkstyle"
    apply plugin: "jacoco"
    apply plugin: "kotlin"
    apply plugin: "kotlin-spring"
    apply plugin: "kotlin-jpa"
    apply plugin: "io.freefair.lombok"
    apply from: "$rootProject.projectDir/gradle/test.gradle"

    generateLombokConfig.enabled = false // Use root config instead

    task allDeps(type: DependencyReportTask) {}

    task sourceJar(type: Jar) {
        // from sourceSets.main.allJava
        // Known issue: with Kotlin sources running publishMavenLocal need to run it twice.
        from sourceSets.main.delombokTask
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_13
        }
    }

    checkstyle {
        config project.resources.text.fromUri("${ritenseCheckStyleLocation}")
        ignoreFailures = false
        showViolations = true
    }

    tasks.withType(Checkstyle) {
        reports {
            html.destination rootProject.file("build/reports/${subproject.project.name}-checkstyle.html")
        }
    }

    sonarqube {
        properties {
            property "sonar.jacoco.reportPaths", allTestCoverageFile
        }
    }

    test.finalizedBy(jacocoMerge)

    jar {
        enabled = true
        manifest {
            attributes("Implementation-Title": projectName)
            attributes("Implementation-Version": projectVersion)
        }
    }

    apply from: "$rootProject.projectDir/gradle/publishing.gradle"
    apply from: "$rootProject.projectDir/gradle/owasp-dependency-check.gradle"

    dependencies {

        implementation "org.springframework.boot:spring-boot-starter"
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "org.springframework.boot:spring-boot-starter-actuator"
        implementation "org.springframework.boot:spring-boot-starter-data-jpa"
        implementation "org.springframework.boot:spring-boot-starter-security"
        implementation "org.springframework.boot:spring-boot-starter-validation"

        implementation "com.fasterxml.jackson.module:jackson-module-afterburner"
        implementation "com.fasterxml.jackson.module:jackson-module-parameter-names"
        implementation "com.fasterxml.jackson.module:jackson-module-jaxb-annotations"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

        //Kotlin core
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        //Logging
        implementation 'io.github.microutils:kotlin-logging:2.0.4'

        api "org.springframework.security:spring-security-core"
        api "org.springframework.boot:spring-boot"
        api "org.springframework.data:spring-data-commons"
        api "javax.inject:javax.inject:1"

        testImplementation('org.junit.jupiter:junit-jupiter')
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation "org.testcontainers:junit-jupiter"

        //Kotlin test
        testImplementation "org.jetbrains.kotlin:kotlin-test"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    }

}